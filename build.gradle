apply plugin: "java"

version = '1.0'

// Use the repo folder name for the jar
def jarName = "Earthquake-and-Asteroid-Mod"   

// Source files
sourceSets.main.java.srcDirs = ["src/EarthquakeAsteroidMod"]

repositories {
    mavenCentral()
    maven { url = "https://raw.githubusercontent.com/Zelaux/MindustryRepo/master/repository" }
    maven { url = 'https://www.jitpack.io' }
}

java {
    targetCompatibility = 8
    sourceCompatibility = JavaVersion.VERSION_17
}

ext {
    mindustryVersion = 'v154.2'
    jabelVersion = "93fde537c7"
    isWindows = System.getProperty("os.name").toLowerCase().contains("windows")
    sdkRoot = System.getenv("ANDROID_HOME") ?: System.getenv("ANDROID_SDK_ROOT")
}

allprojects {
    tasks.withType(JavaCompile).configureEach {
        options.compilerArgs.addAll(['--release', '8'])
    }
}

dependencies {
    compileOnly "com.github.Anuken.Arc:arc-core:$mindustryVersion"
    compileOnly "com.github.Anuken.Mindustry:core:$mindustryVersion"

    annotationProcessor "com.github.Anuken:jabel:$jabelVersion"
}

configurations.configureEach {
    resolutionStrategy.eachDependency { details ->
        if (details.requested.group == 'com.github.Anuken.Arc') {
            details.useVersion "$mindustryVersion"
        }
    }
}

tasks.register('jarAndroid') {
    dependsOn "jar"

    doLast {
        if (!sdkRoot || !new File(sdkRoot).exists())
            throw new GradleException("No valid Android SDK found. Ensure ANDROID_HOME is set.")

        def platformRoot = new File("$sdkRoot/platforms/").listFiles().sort().reverse().find { f ->
            new File(f, "android.jar").exists()
        }

        if (!platformRoot)
            throw new GradleException("No android.jar found. Ensure an Android platform is installed.")

        def dependencies = (configurations.compileClasspath.asList() + configurations.runtimeClasspath.asList() + [new File(platformRoot, "android.jar")]).collect {
            "--classpath $it.path"
        }.join(" ")

        def d8 = isWindows ? "d8.bat" : "d8"

        "$d8 $dependencies --min-api 14 --output ${jarName}Android.jar ${jarName}Desktop.jar"
                .execute(null, new File("build/libs")).waitForProcessOutput(System.out, System.err)
    }
}

jar {
    archiveFileName = "${jarName}Desktop.jar"

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }

    from(projectDir) {
        include "mod.hjson"
    }

    from("assets/") {
        include "**"
    }
}

tasks.register('deploy', Jar) {
    dependsOn jarAndroid
    dependsOn jar

    archiveFileName = "${jarName}.jar"

    from { [zipTree("build/libs/${jarName}Desktop.jar"), zipTree("build/libs/${jarName}Android.jar")] }

    doLast {
        delete {
            delete "build/libs/${jarName}Android.jar"
        }
    }
}
